buildscript {
	ext {
		springBootVersion = '2.1.0.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'io.codurance'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
	mavenCentral()
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-web")
	compile('org.springframework.boot:spring-boot-starter-actuator')
	// implementation('org.springframework.boot:spring-boot-starter-jdbc')
	compile('org.flywaydb:flyway-core')
	runtimeOnly('org.postgresql:postgresql')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.rest-assured:rest-assured:3.2.0')
	testCompile('org.junit.jupiter:junit-jupiter-api:5.3.1')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.3.1')
	testCompile('org.junit.jupiter:junit-jupiter-params:5.3.1')
	testCompile('org.hamcrest:hamcrest-library:1.3')
	testCompile('org.assertj:assertj-core:3.11.1')
}

configurations {
	acceptanceCompile.extendsFrom testCompile
	acceptanceRuntime.extendsFrom testRuntime
}

sourceSets {
	acceptance {
		java.srcDir file("src/acceptance/java")
		resources.srcDir     file("src/acceptance/resources")
		compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.acceptanceCompile
		runtimeClasspath = output + compileClasspath + configurations.acceptanceRuntime
	}
}

task acceptanceTest(type: Test) {
	testClassesDirs = sourceSets.acceptance.output.classesDirs
	classpath = sourceSets.acceptance.runtimeClasspath
}

check.dependsOn acceptanceTest